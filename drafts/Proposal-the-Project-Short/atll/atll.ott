% The full Ott spec of Simply Typed Grady.

embed
  {{tex-preamble \usepackage{cmll}
                 \usepackage{relsize} }}

metavar costvar, c ::=
metavar termvar, x, y , z, f ::=
metavar baseAttackVar, b ::=
indexvar index, i , j, k ::=

grammar
  A, B, C, E :: 'A_' ::=
    | b           :: :: Base
    | A (.) B :: :: Para
    {{tex [[A]] \odot [[B]] }}
    | A + B   :: :: Choice
    {{tex [[A]] \sqcup [[B]] }}
    | A > B   :: :: Seq
    | A -o B  :: :: Limp
    | A o-o B :: :: BLimp
    | ( A )   :: :: Paren
    | h( A ) ::  :: HParen
    {{tex [[A]] }}

  T :: 'T_' ::=
    | b           :: :: Base
    | T1 (.) T2 :: :: Para
    {{tex [[T1]] \odot [[T2]] }}
    | T1 > T2   :: :: Seq
    {{tex [[T1]] \rhd [[T2]] }}
    | T1 + T2   :: :: Choice
    {{tex [[T1]] \sqcup [[T2]] }}
    | ( T ) :: :: Parens

  G {{ tex \Gamma }}, D {{tex \Delta }},
  I {{ tex \Theta }}, H {{tex \Phi}}, P {{tex \Psi }} :: G_ ::=                 
    | .                   ::   :: emptyCtx     
    {{tex \cdot }}
    | A                   ::   :: vn           
    | G , G'              ::   :: append       
    | ( G )               :: M :: parens

  terminals :: 'terminals_' ::=
    | (.) :: :: Para
    {{tex \odot}}
    | > :: :: Seq
    {{tex \rhd}}
    | + :: :: Choice
    {{tex \sqcup }}
    |  ->                 ::   :: arrow
    {{tex \rightarrow }}
    |  <-                 ::   :: arrow2
    {{tex \leftarrow }}
    |  -o                 ::   :: arrow3
    {{tex \multimap }}
    |  o-o                 ::   :: arrow4
    {{tex \multimapboth }}        
    | |-                  ::   :: turnstile
    {{tex \vdash }}
    | |~                  ::   :: ndash
    {{tex \not \vdash }}    
    | succ                ::   :: succ
    {{tex \mathsf{succ} }}
    | ~                   ::   :: cons
    {{tex \sim }}
    | ~U                  ::   :: conv
    {{tex \sim_U }}    
    | elem                ::   :: elem
    {{tex \in }}
    | ~>                  ::   :: red
    {{tex \rightsquigarrow }}
    | box        :: :: box
    {{tex \mathsf{box} }}
    | unbox        :: :: unbox
    {{tex \mathsf{unbox} }}
    | fst        :: :: fst
    {{tex \mathsf{fst} }}
    | snd        :: :: snd
    {{tex \mathsf{snd} }}                

  formula :: 'formula_' ::=
    | judgement           ::   :: judgement
    | formula1 && formula2 ::  :: Quad
    {{tex [[formula1]] \quad [[formula2]] }}
    | ( formula )         :: S :: parens
    {{tex [[formula]] }}
    

subrules
 T <:: A

defns
  Typing :: '' ::=

defn
 G |-T E :: :: atv :: 'T_'    
 {{tex [[G]] \vdash^T [[E]] }} by
    
 ------- :: var
 b |-T b

 G |-T T1 && D |-T T2
 -------------------- :: para
 G,D |-T T1 (.) T2

 G |-T T1 && D |-T T2
 -------------------- :: seq
 G,D |-T T1 > T2

 G |-T T1 && D |-T T2
 -------------------- :: choice
 G,D |-T T1 + T2

defn
 I ; H ; P |- E :: :: ateq :: 'E_'    
 {{tex [[I]];[[H]];[[P]] \vdash [[E]] }} by

 ---------- :: var
 .;.;E |- E

 ---------- :: varCC
 .;E;. |- E
 
 ---------- :: varC
 E;.;. |- E

 I1;H1;P1 |- E1  && I2;H2;P2 |- E2
 --------------------------------- :: paraI
 I1,I2;H1,H2;P1,P2 |- E1 (.) E2

 I1;H1;P2 |- E1 (.) E2 && I2;H2;P1,E1,E2,P3 |- E3
 ------------------------------------------ :: paraE
 I1,I2;H1,H2;P1,P2,P3 |- E3

 I1;H1;P1 |- E1  && I2;H2;P2 |- E2
 --------------------------------- :: choiI
 I1,I2;H1,H2;P1,P2 |- E1 + E2

 I1;H2;P1 |- E1 + E2 && I2;H1,E1,E2,H3;P2 |- E3
 ------------------------------------------ :: choiE
 I1,I2;H1,H2,H3;P1,P2 |- E3

 I1;H1;P1 |- E1  && I2;H2;P2 |- E2
 --------------------------------- :: seqI
 I1,I2;H1,H2;P1,P2 |- E1 > E2

 I2;H1;P1 |- E1 > E2 && I1,E1,E2,I3;H2;P2 |- E3
 ---------------------------------------------- :: seqE
 I1,I2,I3;H1,H2;P1,P2 |- E3

 I;H;P1,E1,E2,P2 |- E
 -------------------- :: exP
 I;H;P1,E2,E1,P2 |- E

 I;H1,E1,E2,H2;P |- E
 -------------------- :: exC
 I;H1,E2,E1,H2;P |- E

 I;H1,E1,H2;P |- E2
 --------------------- :: dup
 I;H1,E1,E1,H2;P |- E2

 I;H1,E1,E1,H2;P |- E2
 --------------------- :: cont
 I;H1,E1,H2;P |- E2

 I;H;P,E1 |- E2
 ----------------- :: impI
 I;H;P |- E1 -o E2

 I1;H1;P1 |- E1 -o E2 && I2;H2;P2 |- E1
 -------------------------------------- :: impE
 I1,I2;H1,H2;P1,P2 |- E2

 I2;H1;P1 |- E1 -o E2 && I1;H2;P2 |- E2 -o E3
 -------------------------------------------- :: comp
 I1,I2;H1,H2;P1,P2 |- E1 -o E3

 I1,I2;H;P |- E && D |-T T
 ------------------------- :: weakS
 I1,T,I2;H;P |- E

 I;H1,H2;P |- E && D |-T T
 ----------------------------- :: weakC
 I;H1,T,H2;P |- E

 I;H;P1,P2 |- E && D |-T T
 ------------------------- :: weakP
 I;H;P1,T,P2 |- E

 I,H,P |-T T1 (.) (T2 > T3)
 ----------------------------------------------------------- :: distPara1
 I;H;P |- (T1 (.) (T2 + T3)) o-o ((T1 (.) T2) + (T1 (.) T3))

 I,H,P |-T T1 > (T2 + T3)
 ----------------------------------------------------- :: distSeq1
 I;H;P |- (T1 > (T2 + T3)) o-o ((T1 > T2) + (T1 > T3))

 I |-T (T2 > T3) (.) T1
 ----------------------------------------------------------- :: distPara2
 I;H;P |- ((T2 + T3) (.) T1) o-o ((T2 (.) T1) + (T3 (.) T1))

 I,H,P |-T (T2 + T3) > T1
 ----------------------------------------------------- :: distSeq2
 I;H;P |- ((T2 + T3) > T1) o-o ((T2 > T1) + (T2 > T1))