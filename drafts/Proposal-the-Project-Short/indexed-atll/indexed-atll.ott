% The full Ott spec of Simply Typed Grady.

embed
  {{tex-preamble \usepackage{cmll}
                 \usepackage{relsize} }}

metavar costvar, c ::=
metavar termvar, x, y , z, f ::=
metavar baseAttackVar, b ::=
indexvar index, i , j, k ::=

grammar
  op :: 'O_' ::=
    | (.)op :: :: Para
    {{tex \mathsf{op}_\odot }}
    | >op   :: :: Seq
    {{tex \mathsf{op}_\rhd }}
    | +op  :: :: Choice
    {{tex \mathsf{op}_\sqcup }}
    | -oop :: :: Imp
    {{tex \mathsf{rel}_{[[-o]]} }}
    | o-oop :: :: Imp3
    {{tex \mathsf{rel}_{[[o-o]]} }}    
    | ->op ( c , - ) :: :: LImp2
    {{tex \mathsf{rel}_{[[->]]}([[c]],-) }}
    | <-op ( c , - ) :: :: RImp2
    {{tex \mathsf{rel}_{[[<-]]}([[c]],-) }}
    | -oop ( c , - ) :: :: Imp2
    {{tex \mathsf{rel}_{[[-o]]}([[c]],-) }}
    | o-oop ( c , - ) :: :: BiImp
    {{tex \mathsf{rel}_{[[o-o]]}([[c]],-) }}            

  C :: 'C_' ::=
    | c :: :: cost
    | op ( C1 , C2 ) :: :: op
    | eta( b ) :: M :: costMap
    {{tex \eta([[b]]) }}

  T :: 'T_' ::=
    | b           :: :: Base
    | T1 (.) T2 :: :: Para
    {{tex [[T1]] \odot [[T2]] }}
    | T1 > T2   :: :: Seq
    {{tex [[T1]] \rhd [[T2]] }}
    | T1 + T2   :: :: Choice
    {{tex [[T1]] \sqcup [[T2]] }}
    | ( T ) :: :: Parens

  E :: 'E_' ::=
    | b           :: :: AttackTree
    | E1 (.) E2 :: :: Para
    {{tex [[E1]] \odot [[E2]] }}
    | E1 > E2   :: :: Seq
    {{tex [[E1]] \rhd [[E2]] }}
    | E1 + E2   :: :: Choice
    {{tex [[E1]] \sqcup [[E2]] }}
    | E1 c -o E2 :: :: LinImp
    {{tex ([[E1]], [[c]]) \multimap [[E2]] }}
    | E1 o-o E2 :: :: BiLinImp
    {{tex [[E1]] \multimapboth [[E2]] }}    
    | ( E )               :: S :: Parens
    
  % G {{ tex \Gamma }}, D {{tex \Delta }} :: G_ ::=                 
  %   | .                   ::   :: emptyCtx     
  %   {{tex \cdot }}
  %   | ( b , c )           ::   :: vn           
  %   | G , G'              ::   :: append       
  %   | ( G )               :: M :: parens

  G {{ tex \Gamma }}, D {{tex \Delta }},
  I {{ tex \Theta }}, H {{tex \Phi}}, P {{tex \Psi }} :: I_ ::=                 
    | .                   ::   :: emptyCtx     
    {{tex \cdot }}
    | ( E , c )           ::   :: vn           
    | I , P               ::   :: append       
    | ( T )               :: M :: parens

  terminals :: 'terminals_' ::=
    | (.) :: :: Para
    {{tex \odot}}
    | > :: :: Seq
    {{tex \rhd}}
    | + :: :: Choice
    {{tex \sqcup }}
    |  ->                 ::   :: arrow
    {{tex \rightarrow }}
    |  <-                 ::   :: arrow2
    {{tex \leftarrow }}
    |  -o                 ::   :: arrow3
    {{tex \multimap }}
    |  o-o                ::   :: arrow4
    {{tex \multimapboth }}        
    | |-                  ::   :: turnstile
    {{tex \vdash }}
    | |~                  ::   :: ndash
    {{tex \not \vdash }}    
    | succ                ::   :: succ
    {{tex \mathsf{succ} }}
    | ~                   ::   :: cons
    {{tex \sim }}
    | ~U                  ::   :: conv
    {{tex \sim_U }}    
    | elem                ::   :: elem
    {{tex \in }}
    | ~>                  ::   :: red
    {{tex \rightsquigarrow }}
    | box        :: :: box
    {{tex \mathsf{box} }}
    | unbox        :: :: unbox
    {{tex \mathsf{unbox} }}
    | fst        :: :: fst
    {{tex \mathsf{fst} }}
    | snd        :: :: snd
    {{tex \mathsf{snd} }}                

  formula :: 'formula_' ::=
    | judgement           ::   :: judgement
    | formula1 && formula2 ::  :: Quad
    {{tex [[formula1]] \quad [[formula2]] }}
    | ( formula )         :: S :: parens
    {{tex [[formula]] }}
    | G = (.)( T , c ) :: :: CtxEq1
    {{tex [[G]] = \bigodot ([[T]],[[c]]) }}
    | D = >( T , c ) :: :: CtxEq2
    {{tex [[D]] = \mathlarger{\mathlarger{\mathlarger{\rhd} } }([[T]],[[c]]) }}
    | ->op( c1 , c2 ) :: :: LImpRel
    {{tex \mathsf{rel}_[[->]]([[c1]],[[c2]]) }}
    | <-op( c1 , c2 ) :: :: RImpRel
    {{tex \mathsf{rel}_[[<-]]([[c1]],[[c2]]) }}
    | -oop( c1 , c2 ) :: :: ImpRel
    {{tex \mathsf{rel}_[[-o]]([[c1]],[[c2]]) }}
    | o-oop( c1 , c2 ) :: :: ImpRel2
    {{tex \mathsf{rel}_[[o-o]]([[c1]],[[c2]]) }}        

subrules
  T <:: E

defns
  Typing :: '' ::=

defn
 D |-T  C T :: :: type :: 'T_'    
 {{tex [[D]] \vdash^T_{[[C]]} [[T]] }} by
    
 --------------- :: var
 (b,c) |-T c b

 D1 |-T c1 T1 && D2 |-T c2 T2
 -------------------------------- :: para
 D1,D2 |-T (.)op(c1,c2) T1 (.) T2

 D1 |-T c1 T1 && D2 |-T c2 T2
 ---------------------------- :: seq
 D1,D2 |-T >op(c1,c2) T1 > T2

 D1 |-T c1 T1 && D2 |-T c2 T2
 ---------------------------- :: choice
 D1,D2 |-T +op(c1,c2) T1 + T2

defn
 I ; H ; P |- C E :: :: eq :: 'E_'    
 {{tex [[I]];[[H]];[[P]] \vdash_{[[C]]} [[E]] }} by

 --------------- :: var
 .;.;(E,c) |-c E

 --------------- :: varCC
 .;(E,c);. |-c E

 --------------- :: varC
 (E,c);.;. |-c E

 . |-T c T1 (.) (T2 > T3)
 ---------------------------------------------------------- :: distPara1
 .;.;. |-c (T1 (.) (T2 + T3)) o-o ((T1 (.) T2) + (T1 (.) T3))

 . |-T c T1 > (T2 + T3)
 ---------------------------------------------------- :: distSeq1
 .;.;. |-c (T1 > (T2 + T3)) o-o ((T1 > T2) + (T1 > T3))

 . |-T c (T2 > T3) (.) T1
 ---------------------------------------------------------- :: distPara2
 .;.;. |-c ((T2 + T3) (.) T1) o-o ((T2 (.) T1) + (T3 (.) T1))

 . |-T c (T2 + T3) > T1
 ----------------------------------------------------- :: distSeq2
 .;.;. |- c ((T2 + T3) > T1) o-o ((T2 > T1) + (T2 > T1))

 I1;H1;P1 |-c1 E1  && I2;H2;P2 |-c2 E2
 ------------------------------------------ :: paraI
 I1,I2;H1,H2;P1,P2 |-(.)op(c1,c2) E1 (.) E2

 I1;H1;P2 |-(.)op(c1,c2) E1 (.) E2 && I2;H2;P1,(E1,c1),(E2,c2),P3 |-c3 E3
 ------------------------------------------------------------------------ :: paraE
 I1,I2;H1,H2;P1,P2,P3 |-c3 E3

 I1;H1;P1 |-c1 E1  && I2;H2;P2 |-c2 E2
 ------------------------------------------ :: choiI
 I1,I2;H1,H2;P1,P2 |-+op(c1,c2) E1 + E2

 I1;H2;P1 |-+op(c1,c2) E1 + E2 && I2;H1,(E1,c1),(E2,c2),H3;P2 |-c3 E3
 -------------------------------------------------------------------- :: choiE
 I1,I2;H1,H2,H3;P1,P2 |-c3 E3

 I1;H1;P1 |-c1 E1  && I2;H2;P2 |-c2 E2
 --------------------------------------- :: seqI
 I1,I2;H1,H2;P1,P2 |- >op(c1,c2) E1 > E2

 I2;H1;P2 |- >op(c1,c2) E1 > E2 && I1,(E1,c1),(E2,c2),I3;H2;P2 |-c3 E3
 --------------------------------------------------------------------- :: seqE
 I1,I2,I3;H1,H2;P1,P2 |-c3 E3

 I;H;P1,(E1,c1),(E2,c2),P2 |-c E
 ------------------------------- :: exP
 I;H;P1,(E2,c2),(E1,c1),P2 |-c E

 I;H1,(E1,c1),(E2,c2),H2;P |-c E
 ------------------------------- :: exC
 I;H1,(E2,c2),(E1,c1),H2;P |-c E

 I;H1,(E1,c1),H2;P |-c2 E2
 --------------------------------- :: dup
 I;H1,(E1,c1),(E1,c1),H2;P |-c2 E2

 I;H1,(E1,c1),(E1,c1),H2;P |-c2 E2
 --------------------------------- :: cont
 I;H1,(E1,c1),H2;P |-c2 E2
 
 I;H;P,(E1,c1) |-c2 E2 && -oop(c1,c2)
 ------------------------------------ :: impI
 I;H;P |- c2 E1 c1 -o E2

 I1;H1;P1 |- c2 E1 c1 -o E2 && I2;H2;P2 |-c1 E1
 ---------------------------------------------- :: impE
 I1,I2;H1,H2;P1,P2 |-c2 E2 